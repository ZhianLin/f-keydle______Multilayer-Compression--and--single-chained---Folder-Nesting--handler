@echo off
rem -----编码：ANSI-----
setlocal enabledelayedexpansion
title ｜子执行器｜

rem 下面这行看似无意义的命令不是闲着没事写的，是为了防止系统找不到“北”，win10没有这行可能不会有任何问题，
rem 但是一位朋友的win11因为没了这行，配置bandizip版本时找不到当前目录下的bandizip文件在哪，我一开始也
rem 摸不着头脑为什么会找不到bandizip，后来加了这条命令后问题就自己消失了，不得不说这也太玄幻了。
cd /d "%~dp0"

rem 存入当前路径
set target_path=%~dp0
rem 路径末尾可能会自带“\”，下面去掉它。
set t_m_p=!target_path:~-1!
if "%t_m_p%"=="\" (set target_path=!target_path:~0,-1!)


rem 读取【组件】文件夹的绝对路径
for /f "delims=" %%t in (_origin_path_.txt) do (set origin_path=%%t)


rem 统一去掉两侧的双引号
set origin_path=!origin_path:"=!
rem 路径末尾可能会自带“\”，下面去掉它。
set t_m_p=!origin_path:~-1!
if "%t_m_p%"=="\" (set origin_path=!origin_path:~0,-1!)


rem 进入循环
goto folder_process






rem 一、【处理当前目录下所有的文件夹】
:folder_process
cd /d "!target_path!"
rem 把所有者改为所有管理员，为下面的授权铺路
rem 即使里面的项目带有“系统”、“隐藏”属性，且对everyone拒绝一切访问，经过实验，takeown也能起效。
takeown /f "*" /A > nul

rem 由于已给everyone授予目标文件夹（1个）完全控制权限并向下继承“:(CI)(OI)(F)”，此时权限允许进入并列出文件，这里只要重置即可。
rem 即使项目带有“系统”、“隐藏”属性，icacls也可以访问。
icacls "*" /reset /C > nul

rem 防止有隐藏或系统文件（夹）让批处理忽略
attrib -S -H "*" /S /D > nul

rem 统计每次处理文件夹的操作次数，到最后操作数会变成0。
set /a operate_times=0
rem 处理之前的视角下的第1级目录，之前母程序已经取得了完全的访问权限。
for /d %%j in ("*") do (
    
    rem 进入（处理之前的视角下的第2级目录）。
    rem 由于（处理之前的视角下的第1级目录）已给everyone授予完全控制权限并向下继承，此时权限允许进入并列出文件。
    
    cd "%%j"
    if !errorlevel! EQU 0 (
        rem 把所有者改为所有管理员，为下面的授权铺路
        takeown /f "*" /A > nul

        rem 由于（处理之前的视角下的第1级目录）已给everyone授予完全控制权限并向下继承，这里只要重置即可。
        icacls "*" /reset /C > nul

        rem 防止有隐藏或系统文件（夹）让批处理忽略
        attrib -S -H "*" /S /D > nul

        rem 统计每个【子】文件夹中，文件夹的个数。
        set /a num_a=0
        for /d %%k in ("*") do (set /a num_a+=1)
    
        rem 统计每个【子】文件夹中，文件的个数。
        set /a num_b=0
        for %%k in ("*") do (set /a num_b+=1)

        rem 复制检测工具
        if not exist "_压缩文件识别器_.exe" (mklink /h "_压缩文件识别器_.exe" "..\_压缩文件识别器_.exe" > nul)
        if not exist "_压缩文件识别器_.exe" (copy /y "..\_压缩文件识别器_.exe" ".\" > nul)
        rem 统计每个【子】文件夹中，压缩文件的个数。
        set /a num_c=0
        set /a ret=0
        for %%k in ("*") do (
            if "%%k" NEQ "_压缩文件识别器_.exe" (
                "_压缩文件识别器_.exe" "%%k"
                set /a ret=!errorlevel!
                if !ret! EQU 0 (
                    set /a num_c+=1
                ) else if !ret! EQU 1 (
                    ren "%%k" "_t_a_r_g_e_t_.bin"
                    "_压缩文件识别器_.exe" "_t_a_r_g_e_t_.bin"
                    set /a ret=!errorlevel!
                    ren "_t_a_r_g_e_t_.bin" "%%k"
                    if !ret! EQU 0 (set /a num_c+=1)
                )
            )
        )
        rem 删除检测工具防止干扰
        del /f /q "_压缩文件识别器_.exe" > nul
        
        cd..

        set /a pass_card=0
        rem 有一个文件夹，没有文件（当然也没有压缩文件）
        if !num_a! EQU 1 (if !num_b! EQU 0 (set /a pass_card=1))
        rem 没有文件夹，有一个文件，且这个文件就是压缩文件（文件个数等于压缩文件个数）
        if !num_a! EQU 0 (if !num_b! EQU 1 (if !num_b! EQU !num_c! (set /a pass_card=1)))
    

        rem 判断是否只有一个文件夹或只有一个压缩文件，如是，移动到上一级，同时操作数加一。
        rem 空文件夹、含有多个项目的文件夹和含有单个非压缩文件的文件夹不处理
        if !pass_card! EQU 1 (

            set /a operate_times+=1

            rem 此时文件夹“J”注定在之后被删除，进行重命名可以一定程度上解决下一层即将释放的文件（夹）与“J”同名的问题。
            set new_f_name_outside=%%j_!time:~0,2!_!time:~3,2!_!time:~6,5!_!random!_!random!_!random!
            ren "%%j" "!new_f_name_outside!"

            cd "!new_f_name_outside!"
            if !errorlevel! EQU 0 (
                rem 【或】移出单个文件夹
                for /d %%i in ("*") do (
        
                    rem 移出前预测：如有冲突，名称里加上当前时间和3个随机数
                    if exist "..\%%i" (

                        set new_f_name_inside=%%i_【!time:~0,2!-!time:~3,2!-!time:~6,5!_!random!_!random!_!random!】
                        ren "%%i" "!new_f_name_inside!"
                        move /y "!new_f_name_inside!" "..\" > nul

                     rem 移出前预测：无冲突，直接移出
                    ) else (
                        move /y "%%i" "..\" > nul
                    )   
                )

                rem 【或】移出单个文件
                for %%i in ("*") do (
            
                    rem 移出前预测：如有冲突，名称里加上当前时间和3个随机数
                    if exist "..\%%i" (
                
                        rem 文件有扩展名和文件名，需要特殊对待。
                        set new_f_name_inside=%%~ni_【!time:~0,2!-!time:~3,2!-!time:~6,5!_!random!_!random!_!random!】%%~xi
                        ren "%%i" "!new_f_name_inside!"
                        move /y "!new_f_name_inside!" "..\" > nul

                     rem 移出前预测：无冲突，直接移出
                    ) else (
                        move /y "%%i" "..\" > nul
                    )
                )

                cd..

                rem 删除已经被处理搬空的文件夹（处理之前的视角下的第1级目录）
                rd /s /q "!new_f_name_outside!"
            )
        )
    )
)

rem 操作数不为零，说明文件夹的处理没结束；为零的话，还得看看有没有压缩文件需要解压缩。
if !operate_times! GTR 0 (
    goto folder_process
) else (
    rem 统计【目标】文件夹下压缩文件个数（刚才只处理了文件夹（里面的文件或文件夹），没有动这里的文件）
    cd /d "!target_path!"
    set /a num_c=0
    set /a ret=0
    for %%k in ("*") do (
        set EXT=%%~xk
        if "!EXT!" NEQ ".【_解压失败_】" (if "%%k" NEQ "_压缩文件识别器_.exe" (if "%%k" NEQ "_origin_path_.txt" (if "%%k" NEQ "_password_.txt" (if "%%k" NEQ "%~nx0" (if "%%k" NEQ "_folder_names_record_.txt" (if "%%k" NEQ "_rm_list_.txt" (
                "_压缩文件识别器_.exe" "%%k"
                set /a ret=!errorlevel!
                if !ret! EQU 0 (
                    set /a num_c+=1
                ) else if !ret! EQU 1 (
                    ren "%%k" "_t_a_r_g_e_t_.bin"
                    "_压缩文件识别器_.exe" "_t_a_r_g_e_t_.bin"
                    set /a ret=!errorlevel!
                    ren "_t_a_r_g_e_t_.bin" "%%k"
                    if !ret! EQU 0 (set /a num_c+=1)
                )
        )))))))
    )

    rem 如果个数大于0，意味着有压缩文件。
    if !num_c! GTR 0 (
        goto compressed_files_process
    ) else (
        rem 没有压缩文件，上一步也确认过没有可以处理的文件夹了，就可以收尾了。
        goto close_up
    )
)





rem 二、【处理压缩文件】
:compressed_files_process
rem 启动解压工具
cd /d "!origin_path!\extracter"
start "【解压操作中】" /min /wait "extracter.exe" 1 "!target_path!"
rem 如果出现多层压缩，还得进行文件夹处理。最后会从“文件夹处理”判断并跳出循环。
cd /d "!target_path!"
goto folder_process





:close_up
cd /d "!target_path!"
rem 将bat和必要的文件复制继承到下一级文件夹中（排除空文件夹），挨个启动每个文件夹中的bat，继续下一代的尝试处理。
for /d %%j in ("*") do (
    cd "%%j"
    if !errorlevel! EQU 0 (
        rem 统计每个文件夹里的项目总数（此时文件访问权限是没问题的）
        set /a num_c=0
        for /d %%k in ("*") do (set /a num_c+=1)
        for %%k in ("*") do (set /a num_c+=1)

        rem 如果不是空文件夹，就执行操作
        if !num_c! NEQ 0 (
            rem 复制各类信息配置和可执行文件
            rem bandizip、任务栏刷新工具、【组件】文件夹的绝对路径固定，如果已经存在（主程序分配下一个密码的时候），就不需要覆盖。
            rem 先尝试硬链接（少占空间），如果失败，换拷贝。
            if exist "_origin_path_.txt" (del /f /q "_origin_path_.txt" > nul)
            mklink /h "_origin_path_.txt" "..\_origin_path_.txt" > nul
            if not exist "_origin_path_.txt" (copy /y "..\_origin_path_.txt" ".\" > nul)

            rem 检测工具
            if not exist "_压缩文件识别器_.exe" (mklink /h "_压缩文件识别器_.exe" "..\_压缩文件识别器_.exe" > nul)
            if not exist "_压缩文件识别器_.exe" (copy /y "..\_压缩文件识别器_.exe" ".\" > nul)

            rem 如果存在旧密码，需要覆盖。先删除后拷贝，直接拷贝会拒绝访问。
            if exist "_password_.txt" (del /f /q "_password_.txt" > nul)
            mklink /h "_password_.txt" "..\_password_.txt" > nul
            if not exist "_password_.txt" (copy /y "..\_password_.txt" ".\" > nul)

            rem 旧删除列表也是一样
            if exist "_rm_list_.txt" (del /f /q "_rm_list_.txt" > nul)
            mklink /h "_rm_list_.txt" "..\_rm_list_.txt" > nul
            if not exist "_rm_list_.txt" (copy /y "..\_rm_list_.txt" ".\" > nul)

            rem 指“_child_excuter_1.bat”自身
            if not exist "%~nx0" (mklink /h "%~nx0" "..\%~nx0" > nul)
            if not exist "%~nx0" (copy /y "..\%~nx0" ".\" > nul)


            rem 记录文件"_folder_names_record_.txt"可自动生成，无需复制。


            rem 启动刚刚复制过来的“子”执行器
            start "｜子执行器｜" /min /wait "%~nx0"
        )
        cd..
    )
)
rem 执行清理工作
cd /d "!target_path!"
del /f /q "_password_.txt" >nul 2>nul
del /f /q "_origin_path_.txt" >nul 2>nul
del /f /q "_folder_names_record_.txt" >nul 2>nul
del /f /q "_压缩文件识别器_.exe" >nul 2>nul
rem "_current_path_.txt"被移走到detector目录下了，不需要清理

rem 进行用户定义的删除指定文件操作
cd /d "%origin_path%\extracter"
start "【删除指定文件中】" /min /wait "extracter.exe" 3 "!target_path!"

cd /d "!target_path!"
del /f /q "_rm_list_.txt" >nul 2>nul

rem 到这里一轮处理已经完成并走出循环了，可以放心地将原来打上“解压失败”的文件改回来，方便下一次尝试。
for %%j in ("*【_解压失败_】") do (
    set replace_name=%%j
    set replace_name=!replace_name:.【_解压失败_】=!
    set replace_name=!replace_name:【_解压失败_】=!
    set replace_name=!replace_name:【_dot_】=.!
    ren "%%j" "!replace_name!"
)
rem 所有文件夹都处理完后，结束。
exit


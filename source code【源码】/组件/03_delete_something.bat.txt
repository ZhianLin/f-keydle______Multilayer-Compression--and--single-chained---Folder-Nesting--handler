@echo off
rem -----编码：ANSI-----
setlocal enabledelayedexpansion
title 删除指定文件

rem 存入当前路径
set origin_path=%~dp0
rem 路径末尾可能会自带“\”，下面去掉它。
set t_m_p=!origin_path:~-1!
if "%t_m_p%"=="\" (set origin_path=!origin_path:~0,-1!)

goto receive


:receive
echo 请将需要处理的文件夹拖入窗口（接收文件夹的地址），之后按回车。
echo 如果需要退出，确保看到此消息后直接按回车，不要拖入文件夹或输入任何内容。
set input=<nul
set /p input=
if not defined input (exit)

rem 统一去掉可能存在的两侧的双引号
set target_path=%input:"=%
rem 路径末尾可能会自带“\”，下面去掉它。
set t_m_p=!target_path:~-1!
if "%t_m_p%"=="\" (set target_path=!target_path:~0,-1!)

if exist "!target_path!" (
    cls
    goto process
) else (
    echo 目标文件夹不存在，请重试
    goto receive
)


:process
cd /d "%target_path%"

rem 清除可能存在的残留
del /f /s /q "_bdzip_core_address_.txt" >nul 2>nul
del /f /s /q "_taskbar_refresh_tool_.txt" >nul 2>nul
del /f /s /q "_origin_path_.txt" >nul 2>nul
del /f /s /q "_password_.txt" >nul 2>nul
del /f /s /q "_解压成功失败检测器_.exe" >nul 2>nul
del /f /s /q "_child_excuter_1.bat" >nul 2>nul
del /f /s /q "_child_excuter_2.bat" >nul 2>nul
del /f /s /q "_folder_names_record_.txt" >nul 2>nul
del /f /s /q "_checker_record_.txt" >nul 2>nul

cd /d "%origin_path%"

rem 进行用户定义的删除指定文件操作，读取配置
rem 获取“删除指定文件起始线”的行数
set /a del_f_start_line=0
set /a line=0
for /f "delims=" %%t in (配置解压密码和配置删除指定文件.txt) do (
    set /a line+=1
    if "%%t"=="-------------------- 删除指定文件起始线 --------------------" (set /a del_f_start_line=!line!)
)

rem 获取“删除指定文件终止线”的行数
set /a del_f_end_line=0
set /a line=0
for /f "delims=" %%t in (配置解压密码和配置删除指定文件.txt) do (
    set /a line+=1
    if "%%t"=="-------------------- 删除指定文件终止线 --------------------" (set /a del_f_end_line=!line!)
)

rem 检查错误
set /a detect_err=0
rem 线不存在或被更改
if !del_f_start_line! EQU 0 (set /a detect_err=1)
if !del_f_end_line! EQU 0 (set /a detect_err=1)
rem 顺序颠倒
if !del_f_end_line! LSS !del_f_start_line! (set /a detect_err=1)
rem 如有错误，处理错误。
if !detect_err! NEQ 0 (goto error)

rem 检查两条线间是否有指定删除的内容
set /a have_something_to_delete=0
set /a tmp=!del_f_end_line! - !del_f_start_line!
if !tmp! GTR 1 (set /a have_something_to_delete=1)

rem 如果有，以删除的内容为循环，每次分配一个删除内容，然后操作；如果没有，就可以防止无用的循环。
if !have_something_to_delete! EQU 1 (
    set /a line=0
    for /f "delims=" %%t in (配置解压密码和配置删除指定文件.txt) do (
        set /a line+=1
        rem 起始终止线之间
        if !line! GTR !del_f_start_line! (
            if !line! LSS !del_f_end_line! (
                set del_content=%%t

                rem 删除操作是在目标目录里的，考虑方便，直接借鉴了密码循环的那部分。
                cd /d "%target_path%"
                if !errorlevel! EQU 0 (
                    rem 删除指定文件
                    del /f /s /q "!del_content!" >nul 2>nul
                )

                rem 循环for里引用的“配置解压密码和配置删除指定文件.txt”在这个bat文件的目录里，
                rem 这里不清楚for里面的t变量是否是一开始就全部从文件中读取、列出安排好的，所以要返回原目录。
                cd /d "%origin_path%"
            )
        )    
    )
    
)
cls
goto receive

:error
echo 配置文件发生错误，请检查“配置解压密码和配置删除指定文件.txt”中的4条线，
echo 或者可以直接【重置配置】以恢复正常，接下来按任意键退出。
@pause>nul
exit

